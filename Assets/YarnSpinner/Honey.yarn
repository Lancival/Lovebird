title: Honey
---
<<declare $metHoney = false as bool>>
<<declare $honeyQuestFinished = false as bool>>
<<declare $honeyQuestCheck = false as bool>>

<<CheckQuest "Honey's Quest" $honeyQuestCheck>>

<<if $metHoney == false>>
    <<jump HoneyFirstMeeting>>
<<elseif $honeyQuestFinished == false && $honeyQuestCheck == false>>
    Honey: I can’t wait to make my kids scrumptious fruit pastries! #line:0ab3bc7 
<<elseif $honeyQuestFinished == false>>
    <<jump HoneyQuestFinish>>
<<else>>
    Honey: Feel free to stop by any time. You’re always welcome in my home! #line:05e2dba 
<<endif>>
===

title: HoneyFirstMeeting
---
<<set $metHoney = true>>
<<AddQuest "Honey's Quest">>

Honey: Oh my, I was told someone new was moving in, but I didn't expect them to be so young! Hello, sweetheart, I'm Honey.  #line:096d017 
Honey: Did you just get here? My apologies, I'm still wearing my work clothes. #line:0e4b903 
Honey: When you farm all day, you're bound to get your clothes dirty. #line:06a1c85 
Honey: Are you here to learn about the wondrous world of farming? #line:0e36fbe 
    ->Yes! #line:0c69f3a 
        Honey: Amazing! Let me show you the basics. #line:0f744dd 
    ->Why would I want to farm? #line:0848cb1 
        Honey: We believe the way to our community’s heart is through our stomach! Why not fill our stomachs with freshly grown produce? #line:0594af4 
Honey: We have many types of unique fruits and vegetables, and they all grow from different kinds of seeds. #line:0fbcce9 
Honey: Plant your seeds on your home island, water them, wait, and then harvest them. I told you it was simple, right? #line:0444c78 
Honey: I've been farming ever since my kids were born, and I love that we get the first pick of fresh produce. #line:05b5bd5 
Honey: The early mornings are difficult at first, but you eventually grow to cherish seeing the sunrise every day. #line:0df85bd 
Honey: Steady income, beautiful scenery, and yummy food – what more could you ask for? #line:0ac3881 
Honey: If you grow any crops you don't consume, you can also sell them for sand dollars. #line:0046f56 
Lume: ? #line:040cf3a 
Honey: No one has told you yet? Sand dollars are our local currency! #line:010c108 
Honey: We have a shopkeeper around here who sells many valuable items that you can buy with sand dollars. #line:00c8f7f 
Honey: Recently, I've been selling all of my produce because we have to pay for my kid's apprenticeship. #line:0bc5298 
Honey: They recently moved out. One day, they were the size of a pistachio. The next, they enter the world all on their own. #line:071dce9 
Honey: I still remember when Muffin "graduated" from elementary school. They had all the children dress up in graduation gowns, how adorable! #line:063c83b 
Honey: Muffin got an award for most books read that year. #line:07421a7 
Honey: I miss preparing my kids' favorite fruit tart after work, but I'm currently not growing any sugarflies because Warbleberry sells the best. #line:06b69cf 
Honey: My kids are coming home soon for the holidays, and I want to reward them for studying so diligently. #line:049e7c7 
Honey: If only I could find someone willing to grow sugarfly... #line:01cfdfe 
Lume: :) #line:09c6717 
Honey: You would do it for me? #line:0496a96 
Honey: I would be so grateful! #line:00ae3db 
Honey: Muffin is working so hard at his apprenticeship, and this would bring them so much joy! #line:05567ce 

<<AddMoney 6>>

Honey: I don't have any sugarfly seeds on me, but here are some sand dollars. #line:0fe1f19 
Honey: You can buy seeds from the shopkeeper; this should be enough to cover the cost. #line:0d9ba95 
Honey: I'll need two sugarflies to bake the pastries. #line:030ab36 
===

title: HoneyQuestFinish
---
<<set $honeyQuestFinished = true>>
<<FinishQuest "Honey's Quest">>
<<AddItem "Sugarfly" -2>>

Honey: Golly! My kids will be oh so grateful. #line:05e3bfb 
Honey: Thank you Lume.  #line:0e8ea02 
Lume: :) #line:00612d3 
Honey: How can I possibly repay you? #line:0f36d81 
Honey: Here are a few sand dollars, my treat. #line:013ee1b 
Honey: You can also have this feather I found in my garden. #line:0fbd408 

<<AddItem "Wonder Feather">>
<<AddMoney 10>>

Honey: It’s pretty and reminded me of you! #line:06b0a5a 
Honey: I better get baking, so I will see you around. #line:0f61bd0 
Honey: Feel free to stop by any time. You’re always welcome in my home! #line:0621501 
===
