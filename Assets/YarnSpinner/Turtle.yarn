title: Weed
---
<<declare $metWeed = false as bool>>
<<declare $weedPulled = false as bool>>

<<if $weedPulled>>
    <<jump Turtle>>
<<elseif $metWeed is false>>
    Lume: It's sparkling...
    <<set $metWeed to true>>
<<else>>
    Would you like to pull the weed out?
    -> Yes
        <<set $weedPulled to true>>
        <<PullWeed>>
        <<jump Turtle>>
    -> No
        Lume: (You decided to leave the weed.)
        Lume: ... But it's so pretty...
<<endif>>
===

title: Turtle
---
<<declare $metTurtle = false as bool>>
<<declare $gotPerfumeHint = false as bool>>
<<declare $georgeQuestFinished = false as bool>>

<<declare $hasFishPerfume = false as bool>>
<<declare $numCheddarfin = 0 as number>>
<<declare $numFlutterfly = 0 as number>>
<<declare $numPopsnapper = 0 as number>>

<<if $georgeQuestFinished>>
    <<jump PostQuestDialogue>>
<<endif>>

<<CheckFish>>

<<if $metTurtle is false>>
    <<jump TurtleFirstMeeting>>

<<elseif $numPopsnapper >= 2 and $numCheddarfin >= 2 and $numFlutterfly >= 1>>
    <<jump GeorgeQuestComplete>>

<<elseif $hasFishPerfume is false>>
    <<if $gotPerfumeHint>>
        George: I wonder how Chom Chom is doing in Viridia...
    <<else>>
        <<jump FishPerfumeHint>>
    <<endif>>

<<else>>
    <<if $hasDisappointedGeorge>>
        George: Ahh... steamed POPSNAPPER, fried CHEDDARFIN, marinated FLUTTERFLY...
    <<else>>
        <<jump NotEnoughFish>>
    <<endif>>
<<endif>>

===

title: TurtleFirstMeeting
---
<<ShowTurtle>>

Giant Turtle: Ouch!!!
Lume: !!!
Giant Turtle: Oh man...*yawn*...how long was I asleep?
Giant Turtle: ...
Giant Turtle: *gurgles*
Giant Turtle: ...
Giant Turtle: Oh, hey there. 
George: I am George. 
George: If you haven't noticed yet, I'm down here.
Lume: ???
George: HA HA HA!! 
George: I mean, under your feet. You're standing on my shell.
George: If you don't mind, I'm feeling a bit peckish right now.
George: It's been... a while since I've eaten.
George: You seem a fine, young bird.
George: Would you be willing to help grab a bite for an elderly tortoise such as myself?
    -> Yes
        George: Oh, you are willing to do me this little favor?
    -> No
        George: Oh, you wouldn't mind doing me this little favor?

<<AddQuest "George's Quest">>

George: Thank you so much, little one!
George: Oh my manners, and your name?
Lume: Lume.
George: Thank you, Lume.
George: Let me know if you need any fishing tips, just come to this same place. I'm simply too famished to move at all!
<<set $metTurtle to true>>
===

title: FishPerfumeHint
---
George: Goodday, Lume! How goes the fishing?
George: ... Ah, you don't know where to start.
George: If he's still around, the shopkeeper at Viridia sells good quality FISH PERFUME.
George: His name is Chom Chom.
George: While you're at it, could you send him my regards? We go a long ways back. He is a dear friend.
<<set $gotPerfumeHint to true>>
===

title: NotEnoughFish
---
George: Mm... sashimi...
George: Hi, Lume. Were you able to procure any fish?
George: ...
George: Hmm...
George: It seems that you don't have the right fish.
George: So sorry to bother you with an old man's cravings, but could you come back after you've gotten TWO POPSNAPPERS, TWO CHEDDARFINS, and ONE FLUTTERFLY?
George: Don't worry about hurrying, hunger makes the best spice!
George: HA HA *gurgle* HA!!
George: ...
<<set $hasDisappointedGeorge to true>>
===

title: GeorgeQuestComplete
---
<<set $setGeorgeQuestFinished to true>>

George: *gurgle gurgle gurgle*
George: Lume... *gurgle gurgle*
George: Is that you? *gurgle*
George: ...
George: Oh my goodness! Is that really the fish I asked you for?
George: GOBBLE GOBBLE GOBBLE
George: MUNCH MUNCH MUNCH
George: CHOMP CHOMP CHOMP

<<AddItem "Popsnapper" -2>>
<<AddItem "Cheddarfin" -2>>
<<AddItem "Flutterfly" -1>>

George: ... *BURP*
George: Ah...
George: That was a lovely meal, Lume. Thank you so much. 
George: How could I ever repay you?
George: ...
George: Oh, were you interested in exploring the other islands?
George: It is definitely a bit of a challenge to get to Atlanta Cove... That is an island located underwater.
George: How is it an island if it is underwater?
George: Beats me!
George: But I could show you the way, if you'd like.
George: You do know the legend, right? Help a turtle, and it will take you the brilliant palace of the dragon king!
George: HA HA HA!
George: Not that there is a dragon king there, but it is quite a nice neighborhood.
George: I hope for you to find whatever it is you are looking for on your travels.
George: Don't be afraid to visit this old chap if you are ever in need of a listening ear.
George: Oh, and a gift. I hope you like it.

<<AddItem "Exploration Feather">>
<<UnlockAtlantaCove>>
===

title: PostQuestDialogue
---
George: Goodday, Lume! How goes it?
George: You've never used fish perfume before this?
George: It's neat, isn't it? Back when I was a wee lad no bigger than you, we had to use fishing rods! 
===